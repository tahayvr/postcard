import QtQuick
import qs.Commons
import qs.Ui
import "ui"
import "ui/controls"

BarWidget {
    id: root
    moduleName: "tahayvr.postcard"

    readonly property string icon: root.setting("icon", "󰆟")

    readonly property color fg: root.bar ? root.bar.foreground : Color.popups.text
    readonly property string face: root.bar && root.bar.fontFamily
                                   ? root.bar.fontFamily : Style.font.family

    // Right-click opens this. Left and middle click stay direct: the two
    // things worth reaching for without reading anything.
    readonly property var actions: [
        { glyph: "\u{F0489}", label: "Region", payload: '{"capture":"region"}',
          tip: "Capture a region" },
        { glyph: "\uf2d0",    label: "Window", payload: '{"capture":"windows"}',
          tip: "Capture a window" },
        { glyph: "\u{F0379}", label: "Screen", mode: "fullscreen", delayed: true,
          tip: "Capture the whole screen" },
        { glyph: "\uf121",    label: "Code",   payload: '{"code":true}',
          tip: "Capture selected text" }
    ]

    implicitWidth: button.implicitWidth
    implicitHeight: barSize

    function summon(payload) {
        root.bar.shell.summon(root.moduleName, payload);
    }

    readonly property bool counting: CaptureDelay.remaining > 0

    function choose(payload) {
        menu.open = false;
        root.summon(payload);
    }

    WidgetButton {
        id: button
        anchors.centerIn: parent
        bar: root.bar
        text: root.counting ? String(CaptureDelay.remaining) : root.icon
        tooltipText: root.counting ? "Capturing soon, click to cancel" : "Postcard"
        active: root.counting
        useActiveColor: root.counting
        onPressed: function (mouseButton) {
            // Hiding is what cancels a pending capture, and keeps the
            // shell's idea of whether the overlay is open in step.
            if (root.counting) root.bar.shell.hide(root.moduleName);
            else if (mouseButton === Qt.RightButton) menu.open = !menu.open;
            else if (mouseButton === Qt.MiddleButton) root.summon('{"code":true}');
            else root.summon('{"capture":"smart"}');
        }
    }

    PopupCard {
        id: menu
        anchorItem: button
        bar: root.bar
        contentWidth: menu.fittedContentWidth(Style.space(240))
        contentHeight: menu.fittedContentHeight(body.implicitHeight)

        Column {
            id: body
            width: parent.width
            spacing: Style.space(10)

            // The wordmark is the way in rather than a heading: clicking it
            // opens the editor, which is why there is no row for that.
            Rectangle {
                width: parent.width
                height: Style.spacing.popupRowHeight
                radius: Style.cornerRadius
                color: markHover.hovered
                       ? Style.hoverFillFor(root.fg, Color.accent) : "transparent"

                Wordmark {
                    anchors.left: parent.left
                    anchors.leftMargin: Style.space(8)
                    anchors.verticalCenter: parent.verticalCenter
                    markHeight: Style.space(15)
                    tint: root.fg
                }

                HoverHandler {
                    id: markHover
                    cursorShape: Qt.PointingHandCursor
                }
                TapHandler {
                    acceptedButtons: Qt.LeftButton
                    onTapped: root.choose('{}')
                }
                PanelToolTip {
                    visible: markHover.hovered
                    text: "Open the editor"
                }
            }

            PanelSeparator { foreground: root.fg }

            Column {
                width: parent.width

                Repeater {
                    model: root.actions

                    Item {
                        id: row
                        required property var modelData
                        readonly property bool delayed: modelData.delayed === true
                        width: parent.width
                        height: Style.spacing.popupRowHeight

                        function payload() {
                            if (!row.delayed) return row.modelData.payload;
                            return JSON.stringify({ capture: row.modelData.mode,
                                                    delay: CaptureDelay.seconds });
                        }

                        Rectangle {
                            anchors.left: parent.left
                            anchors.top: parent.top
                            anchors.bottom: parent.bottom
                            anchors.right: row.delayed ? delay.left : parent.right
                            anchors.rightMargin: row.delayed ? Style.space(4) : 0
                            radius: Style.cornerRadius
                            color: hover.hovered
                                   ? Style.hoverFillFor(root.fg, Color.accent) : "transparent"

                            Row {
                                anchors.fill: parent
                                anchors.leftMargin: Style.space(8)
                                anchors.rightMargin: Style.space(8)
                                spacing: Style.space(12)

                                Text {
                                    anchors.verticalCenter: parent.verticalCenter
                                    width: Style.font.icon
                                    horizontalAlignment: Text.AlignHCenter
                                    text: row.modelData.glyph
                                    color: Qt.darker(root.fg, 1.3)
                                    font.family: root.face
                                    font.pixelSize: Style.font.icon
                                }
                                Text {
                                    anchors.verticalCenter: parent.verticalCenter
                                    text: row.modelData.label
                                    color: root.fg
                                    font.family: root.face
                                    font.pixelSize: Style.font.body
                                }
                            }

                            HoverHandler {
                                id: hover
                                cursorShape: Qt.PointingHandCursor
                            }

                            TapHandler {
                                acceptedButtons: Qt.LeftButton
                                onTapped: root.choose(row.payload())
                            }

                            PanelToolTip {
                                visible: hover.hovered
                                text: row.delayed && CaptureDelay.seconds
                                      ? row.modelData.tip + " in " + CaptureDelay.seconds + " seconds"
                                      : row.modelData.tip
                            }
                        }

                        // A sibling rather than a child of the row, so a tap
                        // here cycles the delay without also firing the capture.
                        Rectangle {
                            id: delay
                            visible: row.delayed
                            anchors.right: parent.right
                            anchors.top: parent.top
                            anchors.bottom: parent.bottom
                            width: Style.space(64)
                            radius: Style.cornerRadius
                            color: delayHover.hovered
                                   ? Style.hoverFillFor(root.fg, Color.accent) : "transparent"

                            Row {
                                anchors.centerIn: parent
                                spacing: Style.space(6)

                                Text {
                                    anchors.verticalCenter: parent.verticalCenter
                                    text: "\uf017"
                                    color: CaptureDelay.seconds ? Color.accent : Qt.darker(root.fg, 1.3)
                                    font.family: root.face
                                    font.pixelSize: Style.font.icon
                                }
                                Text {
                                    anchors.verticalCenter: parent.verticalCenter
                                    text: CaptureDelay.label(CaptureDelay.seconds)
                                    color: CaptureDelay.seconds ? Color.accent : root.fg
                                    font.family: root.face
                                    font.pixelSize: Style.font.body
                                }
                            }

                            HoverHandler {
                                id: delayHover
                                cursorShape: Qt.PointingHandCursor
                            }

                            TapHandler {
                                acceptedButtons: Qt.LeftButton
                                onTapped: CaptureDelay.cycle()
                            }

                            PanelToolTip {
                                visible: delayHover.hovered
                                text: "Delay before the screen is captured, click to change"
                            }
                        }
                    }
                }
            }
        }
    }
}
